import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        kotlinVersion = "2.1.0"
        kotlinfVersion = "1.10.2"
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    group = "com.github.fsbarata.kotlin-numeric"
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin("maven-publish")) {
            if (plugins.hasPlugin("org.jetbrains.kotlin.multiplatform")) {
                publishing.publications {
                    maven(MavenPublication) {
                        from(components.kotlin)
                    }
                }
            }
            if (plugins.hasPlugin("java")) {
                publishing.publications {
                    maven(MavenPublication) {
                        from(components.java)
                    }
                }
            }
        }

        tasks.withType(KotlinCompile.class).configureEach {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_9.toString()
            }
        }

        if (plugins.hasPlugin("java")) {
            java {
                sourceCompatibility = JavaVersion.VERSION_1_9
                targetCompatibility = JavaVersion.VERSION_1_9
            }
        }
    }
}
